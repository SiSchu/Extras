plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta12'
}

group = 'net.eloxad'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    implementation("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
    implementation("com.google.inject:guice:7.0.0")
    annotationProcessor("com.google.inject:guice:7.0.0")
    implementation("org.reflections:reflections:0.10.2")
    annotationProcessor("org.reflections:reflections:0.10.2")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    mergeServiceFiles() // optional: if you're using services like SLF4J, etc.

    // Fix for the duplicate META-INF files issue
    exclude("META-INF/LICENSE", "META-INF/LICENSE.txt", "META-INF/NOTICE", "META-INF/DEPENDENCIES", "META-INF/*.kotlin_module", "META-INF/sisu/**")

    // Optional: if you want a cleaner jar
    minimize()

}